#To be inserted @ 80023bc8
.include "../Common.s"
.include "../kex.s"

.macro modulos reg_math, r1, r2
divwu \reg_math,\r1,\r2
mullw \reg_math,\reg_math,\r2
sub \reg_math,\r1,\reg_math
.endm

.set REG_INPUT,0
.set REG_MATH,0
.set REG_PREV_INDEX,4
.set REG_NUM_OF_VEHICLES,6
.set REG_INPUT_CHECK,5
.set REG_NEW_INDEX,27

#check up press
lis REG_INPUT_CHECK,0x0001
addi REG_INPUT_CHECK,REG_INPUT_CHECK,0x0008
and. REG_INPUT_CHECK,REG_INPUT,REG_INPUT_CHECK
bne MOVE_UP

#check down press
lis REG_INPUT_CHECK,0x0002
addi REG_INPUT_CHECK,REG_INPUT_CHECK,0x0004
and. REG_INPUT_CHECK,REG_INPUT,REG_INPUT_CHECK
bne MOVE_DOWN

#check left press
lis REG_INPUT_CHECK,0x0004
addi REG_INPUT_CHECK,REG_INPUT_CHECK,0x0001
and. REG_INPUT_CHECK,REG_INPUT,REG_INPUT_CHECK
bne MOVE_LEFT

#check right press
lis REG_INPUT_CHECK,0x0008
addi REG_INPUT_CHECK,REG_INPUT_CHECK,0x0002
and. REG_INPUT_CHECK,REG_INPUT,REG_INPUT_CHECK
bne MOVE_RIGHT

b END


MOVE_LEFT:
# wrap around 
lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
modulos REG_MATH,REG_NEW_INDEX,REG_INPUT_CHECK
cmpwi REG_MATH, 0
beq WRAP_LEFT

# move left
subi REG_NEW_INDEX,REG_NEW_INDEX,1
b END

MOVE_RIGHT:
# move right
addi REG_NEW_INDEX,REG_NEW_INDEX,1

# clamp max value
cmpw REG_NEW_INDEX, REG_NUM_OF_VEHICLES
bge WRAP_RIGHT

# wrap around 
lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
modulos REG_MATH,REG_NEW_INDEX,REG_INPUT_CHECK
cmpwi REG_MATH, 0
beq WRAP_RIGHT

b END


MOVE_DOWN:
lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
add REG_NEW_INDEX,REG_NEW_INDEX,REG_INPUT_CHECK
cmpw REG_NEW_INDEX, REG_NUM_OF_VEHICLES
bge SET_PREV
b END


MOVE_UP:
lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
sub REG_NEW_INDEX,REG_NEW_INDEX,REG_INPUT_CHECK
cmpwi REG_NEW_INDEX, 0
blt SET_PREV
b END


WRAP_LEFT:
lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
subi REG_INPUT_CHECK,REG_INPUT_CHECK,1
add REG_NEW_INDEX,REG_NEW_INDEX,REG_INPUT_CHECK
cmpw REG_NEW_INDEX, REG_NUM_OF_VEHICLES
bge SET_MAX
b END


WRAP_RIGHT:
cmpw REG_NEW_INDEX, REG_NUM_OF_VEHICLES
bge WRAP_RIGHT_BOTTOM

lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
sub REG_NEW_INDEX,REG_NEW_INDEX,REG_INPUT_CHECK
cmpwi REG_NEW_INDEX, 0
blt SET_MIN
b END


WRAP_RIGHT_BOTTOM:
lwz REG_INPUT_CHECK,OFST_VehicleMetadata(rtoc)
lwz REG_INPUT_CHECK,0x0C(REG_INPUT_CHECK)
divwu REG_NEW_INDEX, REG_NUM_OF_VEHICLES, REG_INPUT_CHECK
mullw REG_NEW_INDEX, REG_NEW_INDEX, REG_INPUT_CHECK
cmpw REG_NEW_INDEX, REG_NUM_OF_VEHICLES
bge WRAP_BOTTOM_EDGE_ROUNDING
b END

WRAP_BOTTOM_EDGE_ROUNDING:
sub REG_NEW_INDEX, REG_NEW_INDEX, REG_INPUT_CHECK
b END

SET_MAX:
mr REG_NEW_INDEX,REG_NUM_OF_VEHICLES
subi REG_NEW_INDEX,REG_NEW_INDEX,1
b END

SET_MIN:
li REG_NEW_INDEX,0
b END

SET_PREV:
mr REG_NEW_INDEX,REG_PREV_INDEX
b END

END:

# update color box for dedede and meta knight
/*mr r27, r3
li r3, 0
li r4, 0
branchl r12, 0x80133f08
mr r3, r27*/

# continue function
branch r12,0x80023dbc